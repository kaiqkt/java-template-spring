plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.openapi.generator' version '7.5.0'
}

group = 'com.kaiqkt'
version = '0.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

springBoot {
    buildInfo()
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/kaiqkt/*")
        credentials {
            username = project.findProperty("gpr.user") as String ?: System.getenv("GPR_USER")
            password = project.findProperty("gpr.key") as String ?: System.getenv("GPR_API_KEY")
        }
    }
}

java.sourceSets.main.java.srcDir("$buildDir/generated/src/main/java")

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set("$rootDir/src/main/resources/static/api-docs.yml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("com.kaiqkt.template.generated.application.controllers")
    modelPackage.set("com.kaiqkt.template.generated.application.dto")
    skipValidateSpec.set(true)
    configOptions.set([
            dateLibrary           : "java8",
            interfaceOnly         : "true",
            useBeanValidation     : "true",
            performBeanValidation: "true",
            enumPropertyNaming    : "UPPERCASE",
            useSpringBoot3        : "true",
            unhandledException    : "true"
    ])
}

dependencies {
    //spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    //metrics
    implementation 'io.micrometer:micrometer-registry-prometheus:1.10.2'

    //tools
    implementation 'com.kaiqkt:springtools-security:1.0.1'
    implementation 'com.kaiqkt:springtools-healthcheck:1.0.0'

    //code generator
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    //database

    //aws

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}
